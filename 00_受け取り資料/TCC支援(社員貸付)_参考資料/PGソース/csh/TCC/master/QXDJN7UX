#!/bin/csh
#　スクリプト名  ： QXDJN7UX
#　 機  能       ： 入金データLOAD
#　 作成者       ： FIP
#
#  修正番号 修正者   修正日時   修正内容
#  000001   hayashi  2007/07/13 ﾛｰﾀﾞｰ後ｲﾝﾃﾞｯｸｽﾁｪｯｸ追加
#
#　 修正履歴
# 項番   管理番号    修正内容                修正者         修正日
# 000002 0807073     非互換対応              NBC.SHIBUICHI  2009/03/29
# 000002 0808052     非互換対応              NBC.SHIBUICHI  2009/03/29

##################################
#           環境設定             #
##################################
set    SHELL_NAME = $0                              # シェル名
setenv JOB_ID       $SHELL_NAME:t                   # ジョブ名
setenv JOBNET_ID   'QXD1D4UX'                    # ジョブネット名
set    LOGFILE    = $USER_LOG_DIR/${JOB_ID}.log     # ログファイル名
set    LOGWRITE   = $EXE_DIR/QUAJ_MSGLOG.BAT        # ログ出力モジュール
setenv USERKEY      'QXDJN6UX'                   # ユーザキー
set    KINOU      = '入金データLOAD'
set    QX_DATA_DIR   = /d051/UFS/data/qxn/alarmdata   #TMP FILE DIR
# 000001 hayashi add 2007/07/13 start
set TABLE_NAME = QXNJ_HOKENKINT            # 対象テーブル名
# 000001 hayashi add 2007/07/13 end

set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"   # 開始メッセージの出力
##################################
# STEP-1 データロード            #
##################################
STEP_01:
set MODULE = 'SQL*LOADER'
$LOGWRITE "$MODULE" I "開始:QXNJ データロード" $USERKEY

#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start
#sqlldr $DB_USERID/$DB_PASSWORD                       \
sqlldr $DB_USERID/$DB_PASSWORD@$ORACLE_SID            \
   control=$LDR_CTRL_DIR/qxnd_nyuukindatat.sql  \
   data=$QX_DATA_DIR/qxnj_loaddata_dat.load              \
   log=$LDR_LOG_DIR/qxnd_nyuukindatat.log                \
    direct=true                                       \
               > $LOGFILE
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi end
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi end

set rc = $status
if ( $rc != 0 ) then
        set MSG = "失敗:QXNJ データロード RC=[$rc]"
        $LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
        exit  1
endif
# 000001 hayashi add 2007/07/13 start
#----------------------------------------------------------#
#   インデックスエラーのチェック
#----------------------------------------------------------#
rm -f ${LOGFILE}2 ${LOGFILE}3

#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start
#sqlplus -S $DB_USERID/$DB_PASSWORD << EOF > ${LOGFILE}2
sqlplus -S $DB_USERID/$DB_PASSWORD@$ORACLE_SID << EOF > ${LOGFILE}2
set lin 200
select index_name,status from user_indexes
       where table_name = '$TABLE_NAME'  and
             status     = 'UNUSABLE';
quit SQL.SQLCODE
EOF
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi end
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi end

set rc = $status
if ( $rc != 0 ) then
    set MSG = "失敗:データロード(sqlplus) RC=[$rc]"
    $LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
    exit  1
endif
grep "UNUSABLE" `find ${USER_LOG_DIR} -name "${JOB_ID}.log2" -print` > ${LOGFILE}3
if !(-z ${LOGFILE}3 ) then
    set MSG = "失敗:データロード(grep) RC=[$rc]"
    $LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
    exit  1
endif
# 000001 hayashi add 2007/07/13 end
$LOGWRITE "$MODULE" I "終了:QXNJ データロード" $USERKEY

####################################################
# STEP-2 処理コントロールテーブルロード            #
####################################################
STEP_02:
set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "開始:処理コンロード(事故売上)" $USERKEY
/usr/local/auto/master/QSDJYLUX
/usr/local/auto/master/QSDJFLUX
/usr/local/auto/master/QSDJ1JUX
$LOGWRITE "$MODULE" I "終了:処理コンロード(事故売上)" $USERKEY

##########################################
# STEP-99      後始末                    #
##########################################
STEP_99:

set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"   # 終了メッセージの出力
exit 0

