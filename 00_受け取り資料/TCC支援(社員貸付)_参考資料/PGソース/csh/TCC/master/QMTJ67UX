#!/bin/csh
#　スクリプト名  ： QMTJ67UX
#　 プロセス     ： 抽選結果取込
#　 機  能       ： 抽選結果取込
#　 作成者       ： FIP
#　 更新者       ： 
##########################################
#         共通環境設定                   #
##########################################
set    SHELL_NAME = $0                                      # シェル名
setenv JOB_ID       $SHELL_NAME:t                           # ジョブ名
setenv JOBNET_ID    'QMT408UX'                              # ジョブネット名
set    LOGFILE    = $USER_LOG_DIR/${JOB_ID}.log             # ログファイル名
set    LOGWRITE   = $EXE_DIR/QUAJ_MSGLOG.BAT                # ログ出力モジュール
setenv USERKEY      'QMTJ67UX'                              # ユーザキー
set    KINOU      = 'ポイント付与・抽選結果取込'

##########################################
#         固有環境設定                   #
##########################################
set    HULDIR     = /d321/UFS/fromhost/qma/rcv
setenv HULFILE      $HULDIR/OFQWB3.hft
setenv BAKFILE      $HULDIR/OFQWB3.bak
set    DATDIR     = /d321/UFS/data/qma
setenv INFILE1      $DATDIR/qmab_s001000_IN1.dat
setenv OTFILE1      $DATDIR/qmab_s001000_OT1.dat
setenv OTFILE2      $DATDIR/qmab_s001000_OT2.dat

##########################################
#         処理開始メッセージ             #
##########################################
set MODULE        = 'SHELL'                                 # モジュール
$LOGWRITE "$MODULE" I "開始：$KINOU" "$USERKEY"             # 開始メッセージの出力

##########################################
# STEP-1  データ移動                     #
##########################################
STEP_1:
set    MODULE     = cp                                      # モジュール名
set STEPKINOU     = 'ポイント付与会員Ｆ（当選者情報）コピー'

 cp -p  $HULFILE  $INFILE1

set rc = $status
if ( $rc != 0 ) then
    $LOGWRITE  "$MODULE"  E  "失敗：$KINOU  RC=[$rc]"  "$USERKEY"
    exit(1)
endif

##########################################
# STEP-2  当選者情報取込                 #
##########################################
STEP_2:
set MODULE        = 'QMAB_S001000'
set STEPKINOU     = '当選者情報取込'
 $LOGWRITE "$MODULE" I "開始：$STEPKINOU" "$USERKEY"        #開始メッセージの出力
set PROGRAM       = $EXE_DIR/qmab_s001000.exe
if ( -x $PROGRAM ) then
    setenv NLS_LANG  Japanese_Japan.JA16SJIS                #文字コード体系をSJISにする
    $PROGRAM      >&   $LOGFILE                             #プログラム起動
    set rc = $status                                        #リターンステータス退避
     setenv NLS_LANG  Japanese_Japan.JA16EUC                #文字コード体系をEUCに戻す
else
    $LOGWRITE  "$MODULE"  E  "実行プログラム[$PROGRAM]が存在しない。" "$USERKEY"
    exit (1)                                                #異常終了
endif

if ($rc != 0) then                                          #リターンステータス判定
    set MSG = "失敗：$STEPKINOU RC=[$rc]"
     $LOGWRITE "$MODULE" E "$MSG" "$USERKEY"
    exit(1)                                                 #異常終了
endif

  $LOGWRITE  "$MODULE" I "終了：$STEPKINOU" "$USERKEY"      #終了メッセージの出力

##########################################
# STEP-3  データバックアップ             #
##########################################
STEP_3:
set    MODULE     = cp                                      # モジュール名
set STEPKINOU     = 'ポイント付与会員Ｆ（当選者情報）バックアップ'

 mv  $HULFILE  $BAKFILE

set rc = $status
if ( $rc != 0 ) then
    $LOGWRITE  "$MODULE"  E  "失敗：$KINOU  RC=[$rc]"  "$USERKEY"
    exit(1)
endif
##########################################
# STEP-4 中間ファイル削除                #
##########################################
STEP_4:
rm $INFILE1

##########################################
# STEP-99  終了メッセージ出力            #
##########################################
STEP_99:
set MODULE        = 'SHELL'
$LOGWRITE "$MODULE" I "終了：$KINOU" "$USERKEY"             # 終了メッセージの出力

exit(0)
