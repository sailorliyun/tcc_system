#!/bin/csh
#
#  日次ＢＣＶバックアップ (QSDJB1UX)
#
#     機  能：1.データベースのバックアップをＢＣＶに待避する。
#             2.REDOログファイル、REDOアーカイブログファイルを待避する。
#             3.制御ファイルを待避する。
#     引  数：
#
#     EXIT値：0 --- 正常
#             1 --- エラー発生
# 項番   管理番号    修正内容                  修正者         修正日
# 000001 1409036     仮想環境移行作業（TCC支援 FIP.Eriguchi   2014/11/06
# 000002 2015080034  EMCディスク切替対応       FIP.Tsuji      2015/09/04

set  SHELL_NAME = $0                                    # シェル名
set  MODULE   =  $SHELL_NAME:t                          # モジュール名
set  LOGFILE  =  $USER_LOG_DIR/db_ope.log               # ログファイル名
set  LOGWRITE = "$EXE_DIR/QUAJ_BATCHLOG.BAT ($MODULE)"  # ログ出力用共通シェル
set  SYMCLI_DIR = /usr/symcli/bin
set  DEVICE_FILE = /usr/symmapps/tcc.bcv                # ﾃﾞﾊﾞｲｽファイル名
set  DG_FILE = tcc_dg    		                # ﾃﾞﾊﾞｲｽファイル名

## ADD 000001 仮想環境移行作業（TCC支援） 20141106 FIP.Eriguchi
## MOD 000002 EMCディスク切替対応         20150904 FIP.Tsuji
#setenv SYMCLI_CONNECT       'SYMAPI_SERVER'  # 仮想化基盤サーバ経由_EMC接続設定（IT運用G指示）
setenv SYMCLI_CONNECT       'sueswl'
setenv SYMCLI_CONNECT_TYPE  'REMOTE'         # 仮想化基盤サーバ経由_EMC接続設定
setenv SYMCLI_CTL_ACCESS    'PARALLEL'       # 仮想化基盤サーバ経由_EMC接続設定
## ADD 000002 EMCディスク切替対応         20150904 FIP.Tsuji
setenv SYMCLI_WAIT_ON_DB     1
setenv SYMCLI_WAIT_ON_GK     1

$LOGWRITE  "日次ＢＣＶバックアップ開始" >> $LOGFILE

###############################
# 1. データベースバックアップ #
###############################
$LOGWRITE  "BCVへのﾎﾞﾘｭｰﾑｺﾋﾟｰ開始" >> $LOGFILE

### ESTABLISH ###
$SYMCLI_DIR/symmir -g $DG_FILE establish -noprompt  # BCV へのﾎﾞﾘｭｰﾑｺﾋﾟｰ
#bcv est -i -f $DEVICE_FILE  >> $LOGFILE  # BCVへのﾎﾞﾘｭｰﾑｺﾋﾟｰ
#bcv est -f $DEVICE_FILE  >> $LOGFILE  # BCVへのﾎﾞﾘｭｰﾑｺﾋﾟｰ
set RC = $status                         # ﾘﾀｰﾝｽﾃｰﾀｽ待避

if ( $RC != 0 ) then
	$LOGWRITE "ESTABLISH失敗 \[RC=$RC\]"   >> $LOGFILE
	exit 1
endif

### ｺﾋﾟｰ完了監視 ###
while(1)
	$SYMCLI_DIR/symmir -g $DG_FILE verify > /dev/null     # ｺﾋﾟｰ完了の確認
#	bcv verify -f $DEVICE_FILE  > /dev/null   # ｺﾋﾟｰ完了の確認
	set RC = $status                          # ﾘﾀｰﾝｽﾃｰﾀｽ待避
	if ( $RC == 0 ) then
		break
	else if ( $RC != 4 && $RC != 5 ) then
		$LOGWRITE "VERIFY失敗 \[RC=$RC\]"   >> $LOGFILE
		exit 1
	endif
	sleep 60                          # １分間待機
end
	
### Standard Volume UNMOUNT ###
$EXE_DIR/QUAJ_VOLOPE.BAT u
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
	$LOGWRITE "STD VOL アンマウント失敗 \[RC=$RC\]"   >> $LOGFILE
	exit 1
endif

### SPLIT ###
$SYMCLI_DIR/symmir -g $DG_FILE split -noprompt # Standard VolumeをBCVからの切り離す
#bcv split -f $DEVICE_FILE         # Standard VolumeをBCVからの切り離す
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
	$LOGWRITE "SPLIT失敗 \[RC=$RC\]"   >> $LOGFILE
	exit 1
endif

### tandard Volume MOUNT ###
$EXE_DIR/QUAJ_VOLOPE.BAT m
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
	$LOGWRITE "STD VOL マウント失敗 \[RC=$RC\]"   >> $LOGFILE
	exit 1
endif

$LOGWRITE  "BCVへのﾎﾞﾘｭｰﾑｺﾋﾟｰ正常終了" >> $LOGFILE

#######################################################
# 2. REDOオンラインログ & アーカイブログファイル待避  #
#######################################################
### 正 ###
$LOGWRITE  "REDOログファイル(正)待避開始" >> $LOGFILE

set CNT = `ls -1 /d241/UFS/save_sei/DBF/qs_tcc/arch | wc -l`
if ( $CNT > 0 ) then
    rm /d241/UFS/save_sei/DBF/qs_tcc/arch/*
    set RC = $status                      # ﾘﾀｰﾝｽﾃｰﾀｽ待避
    if ( $RC != 0 ) then
        $LOGWRITE "REDO ARCHIVE in /d241-sei 削除失敗 \[RC=$RC\]"   >> $LOGFILE
        exit 1
    endif
endif

cp -pR /d230/DBF /d241/UFS/save_sei
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
	$LOGWRITE "REDOログファイル(正)待避失敗 \[RC=$RC\]"   >> $LOGFILE
	exit 1
endif

$LOGWRITE  "REDOログファイル(正)待避正常終了" >> $LOGFILE

### 副 ###
$LOGWRITE  "REDOログファイル(副)待避開始" >> $LOGFILE

set CNT = `ls -1 /d231/UFS/save_huku/DBF/qs_tcc/arch | wc -l`
if ( $CNT > 0 ) then
    rm /d231/UFS/save_huku/DBF/qs_tcc/arch/*
    set RC = $status                      # ﾘﾀｰﾝｽﾃｰﾀｽ待避
    if ( $RC != 0 ) then
        $LOGWRITE "REDO ARCHIVE in /d231-huku 削除失敗 \[RC=$RC\]"   >> $LOGFILE
        exit 1
    endif
endif

cp -pR /d240/DBF /d231/UFS/save_huku
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
	$LOGWRITE "REDOログファイル(副)待避失敗 \[RC=$RC\]"   >> $LOGFILE
	exit 1
endif

$LOGWRITE  "REDOログファイル(副)待避正常終了" >> $LOGFILE

###############################
# 3. 制御ファイル待避         #
###############################
### 正 ###
$LOGWRITE  "制御ファイル(正)待避開始" >> $LOGFILE

cp -p /d011/DBF/qs_tcc/control.ctl /d241/UFS/save_sei/qs_tcc
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
    $LOGWRITE "tcc制御ファイル(正)待避失敗 \[RC=$RC\]"   >> $LOGFILE
    exit 1
endif

cp -p /d011/DBF/qs_secu/control.ctl /d241/UFS/save_sei/qs_secu
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
    $LOGWRITE "secu制御ファイル(正)待避失敗 \[RC=$RC\]"   >> $LOGFILE
    exit 1
endif

$LOGWRITE  "制御ファイル(正)待避正常終了" >> $LOGFILE

### 副 ###
$LOGWRITE  "制御ファイル(副)待避開始" >> $LOGFILE

cp -p /d030/DBF/qs_tcc/control.ctl /d241/UFS/save_huku/qs_tcc
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
    $LOGWRITE "tcc制御ファイル(副)待避失敗 \[RC=$RC\]"   >> $LOGFILE
    exit 1
endif

cp -p /d030/DBF/qs_secu/control.ctl /d241/UFS/save_huku/qs_secu
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
    $LOGWRITE "secu制御ファイル(副)待避失敗 \[RC=$RC\]"   >> $LOGFILE
    exit 1
endif

$LOGWRITE  "制御ファイル(副)待避正常終了" >> $LOGFILE

##### 日次バックアップ２開始 ############################
/s010/DBOPE/exe/QUDJB2UX
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
        $LOGWRITE "日次バックアップ２失敗 \[RC=$RC\]"   >> $LOGFILE
        exit 1
endif
##### 日次バックアップ２終了 ############################

##### 日次バックアップ３開始 ############################
/s010/DBOPE/exe/QUDJB3UX
set RC = $status                  # ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
        $LOGWRITE "日次バックアップ３失敗 \[RC=$RC\]"   >> $LOGFILE
        exit 1
endif
##### 日次バックアップ３終了 ############################

##### 日次バックアップ４開始 ############################
/s010/DBOPE/exe/QUDJB5UX
set RC = $status                  #ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
        $LOGWRITE "日次バックアップ４失敗 \[RC=$RC\]"   >> $LOGFILE
        exit 1
endif
##### 日次バックアップ４終了 ############################

#20040910ＣＳ対応に伴う追加 FIP.UEHARA
##### 日次バックアップ５開始 ############################
/s010/DBOPE/exe/QUDJB6UX
set RC = $status                  #ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
        $LOGWRITE "日次バックアップ５失敗 \[RC=$RC\]"   >> $LOGFILE
        exit 1
endif
##### 日次バックアップ５終了 ############################
#20040910

#20050929キャッシング明細に伴う追加 NBC.YOSHIKAWA
##### 日次バックアップ６開始 ############################
/s010/DBOPE/exe/QUDJB7UX
set RC = $status                  #ﾘﾀｰﾝｽﾃｰﾀｽ待避
if ( $RC != 0 ) then
        $LOGWRITE "日次バックアップ６失敗 \[RC=$RC\]"   >> $LOGFILE
        exit 1
endif
##### 日次バックアップ６終了 ############################
#20050929

$LOGWRITE  "日次ＢＣＶバックアップ終了" >> $LOGFILE

exit 0
