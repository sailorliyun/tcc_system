#!/bin/csh
#　スクリプト名  ： QLTJV6UX
#　 機  能       ： ＣＩＣクレジットファイルⅡ（更新）登録処理
#　 作成者       ： FIP
#
#　 修正履歴
# 項番   管理番号    修正内容                修正者         修正日

##################################
#           環境設定             #
##################################
set    SHELL_NAME = $0                                 # シェル名
setenv JOB_ID       $SHELL_NAME:t                      # ジョブ名
setenv JOBNET_ID   'QLT061UX'                          # ジョブネット名
set    LOGFILE    = $USER_LOG_DIR/${JOB_ID}.log        # ログファイル名
set    LOGWRITE   = $EXE_DIR/QUAJ_MSGLOG.BAT           # ログ出力モジュール
setenv USERKEY      'QLTJV6UX'                         # ユーザキー
set    KINOU      = 'ＣＩＣクレジットファイルⅡ（更新）登録処理'


set    HULFT_NAME = 'OFQA90'                      	   # HULFT名
set    HULFT_DIR  = '/s030/UFS/fromhost/qv/rcv'        # HULFTファイル格納フォルダー
set    KEEP_FOLDER = $QV_DATA_DIR'/qv/qvz/rcv'		   # 保管フォルダー
set    COBOL_WORK_DIR = $QV_DATA_DIR'/qv/data'		   # COBOLファイル出力フォルダー
set    FILENAME_FILE = 'qvz7_filename'			       # ファイル名格納ファイル名

set    IN_FILE     = $HULFT_DIR/$HULFT_NAME		       # ハルフトファイル
set    IN_FILE_ZIP = $COBOL_WORK_DIR/$HULFT_NAME'.zip'       # ハルフトファイル（圧縮後）
set    OT_FILE     = $COBOL_WORK_DIR/$FILENAME_FILE	   # ファイル名格納ファイル

set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"         # 開始メッセージの出力

#########################################
# STEP-1  HULFT集信ファイルチェック     #
#########################################
if (! -e $IN_FILE) then
	$LOGWRITE "$MODULE" E "集信ファイル存在なし" "$USERKEY"
	exit  1                                                     # 異常終了
endif

if (-z $IN_FILE) then
	$LOGWRITE "$MODULE" I "ファイルが0バイトのため処理を終了します" "$USERKEY"    	# メッセージの出力
	goto end-shell
endif
#########################################
# STEP-2  改行コード変換・圧縮          #
#########################################
cd $HULFT_DIR
cp $HULFT_NAME $HULFT_NAME'.txt' 
zip -l $IN_FILE_ZIP $HULFT_NAME'.txt' 
set rc = $status                            	# リターンステータス待避
if ( $rc != 0 ) then                        	# リターンステータス判定
    $LOGWRITE  "$MODULE"  E  "失敗:改行コード圧縮失敗" "$USERKEY"
    exit  1                                     # 異常終了
endif                            	
cd

#########################################
# STEP-3  ファイルサイズ取得            #
#########################################
set fsize=`wc -c $IN_FILE_ZIP | awk '{print $1}'`
set rc = $status                             	# リターンステータス待避
if ( $rc != 0 ) then                        	# リターンステータス判定
    $LOGWRITE  "$MODULE"  E  "失敗:ファイルサイズ取得失敗" "$USERKEY"
    exit  1                                     # 異常終了
endif

#############################################
# STEP-4  管理テーブル登録 ＆ファイル名取得 #
#############################################
set MODULE = 'COBOL'
$LOGWRITE "$MODULE" I "開始:管理テーブル登録＆ファイル名取得" "$USERKEY"     # 開始メッセージの出力

set PROGRAM = $EXE_DIR/qvzb_s000500.exe
if ( -x $PROGRAM ) then
    #
    #  ファイル識別子の設定
    setenv OTFILE $OT_FILE
    #
    setenv NLS_LANG Japanese_Japan.JA16SJIS      # 文字コード体系をSJISにする
    $PROGRAM $fsize $HULFT_NAME                  # プログラム起動
    set rc = $status                             # リターンステータス待避
    setenv NLS_LANG Japanese_Japan.JA16EUC       # 文字コード体系をEUCに戻す
else
    $LOGWRITE  "$MODULE"  E  "実行プログラム[$PROGRAM]が存在しない。" "$USERKEY"
    exit 1                                       # 異常終了
endif

if ( $rc != 0 ) then                              # リターンステータス判定
    $LOGWRITE  "$MODULE"  E  "失敗:管理テーブル登録＆ファイル名取得 RC=[$rc]" "$USERKEY"
    exit  1                                      # 異常終了
endif

$LOGWRITE "$MODULE" I "終了:管理テーブル登録＆ファイル名取得" "$USERKEY"     # 終了メッセージの出力


#########################################
# STEP-5  報告ファイルコピー            #
#########################################
set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "開始:報告ファイルコピー" "$USERKEY"    		# 開始メッセージの出力

if (! -e $OT_FILE) then
    $LOGWRITE  "$MODULE"  E  "失敗:ファイル名格納ファイルが存在しない" "$USERKEY"
    exit  1                                      				# 異常終了
endif

if (-z $OT_FILE) then
    $LOGWRITE  "$MODULE"  E  "失敗:ファイル名格納ファイルにデータが存在しない" "$USERKEY"
    exit  1                                      				# 異常終了
endif

set f = `cat $OT_FILE`
cp $IN_FILE_ZIP "$KEEP_FOLDER/$f"
set rc = $status
if ( $rc != 0 ) then
	$LOGWRITE  "$MODULE"  E  "失敗:報告ファイルコピー  RC=[$rc]"  "$USERKEY"
	exit  1                                      			# 異常終了
endif

$LOGWRITE "$MODULE" I "終了:報告ファイルコピー" "$USERKEY"    		# 終了メッセージの出力

#########################################
# STEP-99  終了処理                     #
#########################################
end-shell:

$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"     				# 終了メッセージの出力
exit 0                                           				# 正常終了
