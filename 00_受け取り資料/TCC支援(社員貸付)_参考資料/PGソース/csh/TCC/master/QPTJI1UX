#!/bin/csh
#　スクリプト名  ： QPTJI1UX
#　機  能        ： 残高更新データ作成(月次)
#　作成者        ： FIP
#

##################################
#           環境設定             #
##################################
set    SHELL_NAME   = $0                             # シェル名
setenv JOB_ID         $SHELL_NAME:t                  # ジョブ名
setenv JOBNET_ID      'QPTI00UX'                     # ジョブネット名
set    LOGFILE      = $USER_LOG_DIR/${JOB_ID}.log    # ログファイル名
set    LOGWRITE     = $EXE_DIR/QUAJ_MSGLOG.BAT       # ログ出力モジュール
setenv USERKEY        'QPTJI1UX'                     # ユーザキー
set    KINOU        = '残高更新データ作成(月次)'
set    QP_DATA_DIR  = /d051/UFS/data/qp              # TMP FILE DIR
set    QP_HUL_DIR   = /s020/UFS/fromhost/qp/rcv      # TMP FILE DIR
set    MODULE       = 'SHELL'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"       # 開始メッセージの出力
#########################################
# STEP-1 小口入金転送データ作成         #
#########################################

set MODULE = 'COBOL'
setenv GKKBN          2                                # 外部パラメータ・処理区分(1:日次,2:月次)
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"         # 開始メッセージの出力

set PROGRAM = $EXE_DIR/qpab_s000900.exe
if ( -x $PROGRAM ) then
    #
    #  ファイル識別子の設定
    setenv OTFILE $QP_DATA_DIR/zandaka_k_g.hft
    #
    #
    #
    setenv NLS_LANG Japanese_Japan.JA16SJIS          # 文字コード体系をSJISにする
    $PROGRAM  > & $LOGFILE                           # プログラム起動
    set rc = $status                                 # リターンステータス待避
    setenv NLS_LANG Japanese_Japan.JA16EUC           # 文字コード体系をEUCに戻す
else
    $LOGWRITE  "$MODULE"  E  "実行プログラム[$PROGRAM]が存在しない。" "$USERKEY"
    exit 1                                           # 異常終了
endif

if ( $rc != 0 ) then                                 # リターンステータス判定
    $LOGWRITE "$MODULE" E "異常終了:$KINOU" "$USERKEY"     # 終了メッセージの出力
    exit  1                                          # 異常終了
endif

$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"       # 終了メッセージの出力

#########################################
# STEP-2     転送データコピー           #
#########################################

set MODULE = 'SHELL'
set KINOU  = '残高更新データ作成データコピー(月次)'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"       # 開始メッセージの出力

cp -f $QP_DATA_DIR/zandaka_k_g.hft $QP_HUL_DIR/zandaka_k_g.hft
set rc = $status

if ( $rc != 0 ) then
    $LOGWRITE  "$MODULE"  E  "異常終了:$KINOU  RC=[$rc]"  "$USERKEY"
    exit  1                                          # 異常終了
endif

$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"       # 終了メッセージの出力

##########################################
# STEP-99      後始末                    #
##########################################

set MODULE = 'SHELL'
set KINOU  = '残高更新データ作成(月次)'
$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"       # 終了メッセージの出力
exit 0                                               # 正常終了

