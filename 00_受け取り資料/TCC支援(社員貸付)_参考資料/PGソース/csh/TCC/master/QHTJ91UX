#!/bin/csh
#　スクリプト名  ： QHTJ91UX
#　 機  能       ： 新ユニット番号変更最古年月吸い上げ処理
#　 作成者       ： FIP
#　 新規         ： 04/11/16 
# 項番     修正日      修正者         修正内容
# 00001    2006/06/14  IMZ.YOSHIDA    JOB NET ID変更

####################################################
#      環境設定                                    #
####################################################
set    SHELL_NAME = $0                             # シェル名
setenv JOB_ID       $SHELL_NAME:t                  # ジョブ名
# 00001 IMZ.YOSHIDA 2006/06/14 start
#setenv JOBNET_ID   'QHT641UX'                      # ジョブネット名
setenv JOBNET_ID   'QHT751UX'                      # ジョブネット名
# 00001 IMZ.YOSHIDA 2006/06/14 end
set    LOGFILE    = $USER_LOG_DIR/${JOB_ID}.log   # ログファイル名
set    LOGWRITE   = $EXE_DIR/QUAJ_MSGLOG.BAT       # ログ出力モジュール
setenv USERKEY      'QHTJ91UX'                     # ユーザキー 
set    KINOU      = '新ユニット番号変更最古年月吸い上げ'

set    QH_SHIFT_DIR = $QH_DATA_DIR/shift
set    QH_SAVE_DIR  = /d051/UFS/data/qh/shift     #本番 SHIFT SAVE DIR

set    STEP_F       = $QH_DATA_DIR/${JOB_ID}_STEP_.tmp

set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"          #開始メッセージの出力
set year  = `date +%Y`        # 処理年 (システム日付より)
set month = `date +%m`        # 処理月 (システム日付より)

# 処理月Leading Zero Suppress 
@ month ++
@ month --

# 前月算出
set zen_year  = $year
set zen_month = $month
if ( $zen_month == 1 ) then
	@ zen_year --
	set zen_month = 12
else
	@ zen_month --
endif

if ( $zen_month < 10 ) then
	set zen_month = 0$zen_month
endif

if ( $month < 10 ) then
	set month = 0$month
endif

if ( ! -e $STEP_F ) then
    touch $STEP_F
endif 
####################################################
#      STEP-1  最古年月吸上                        #
####################################################
set MODULE = 'qheb_s000700'
set STEPKINOU = '請求情報 最古年月吸上'
$LOGWRITE "$MODULE" I "開始:$STEPKINOU" "$USERKEY"      # 開始メッセージの出力

#rm -f $QH_SAVE_DIR/qhef_seikyuunit01.dat.Z

setenv OUT1FIL1 $QH_SAVE_DIR/qhef_seikyuunit01.dat

set PROGRAM = $EXE_DIR/qheb_s000700.exe
if ( -x $PROGRAM ) then
    setenv NLS_LANG Japanese_Japan.JA16SJIS        # 文字コード体系をSJISにする
    $PROGRAM       >&  $LOGFILE                    # プログラム起動
    set rc = $status                               # リターンステータス待避
    setenv NLS_LANG Japanese_Japan.JA16EUC         # 文字コード体系をEUCに戻す
else
    $LOGWRITE  $PROGRAM:t  E  '実行プログラム[$PROGRAM]が存在しない。'
    exit 1                                         # 異常終了
endif

if ( $rc != 0 ) then                               # リターンステータス判定
    set MSG = "失敗:$STEPKINOU RC=[$rc]"
    $LOGWRITE      "$MODULE"  E  "$MSG" $USERKEY
    exit  1                                        # 異常終了
endif
####################################################
#      ファイル圧縮                                #
####################################################
#compress -f $QH_SAVE_DIR/qhef_seikyuunit01.dat

$LOGWRITE "$MODULE" I "終了:$STEPKINOU" "$USERKEY"      # 終了メッセージの出力

####################################################
# STEP-99      後始末                              #
####################################################
STEP_END_PROC:

/bin/rm $STEP_F

set MODULE = 'SHELL       '
$LOGWRITE  "$MODULE" I "終了:$KINOU" "$USERKEY"         # 終了メッセージの出力

exit 0
