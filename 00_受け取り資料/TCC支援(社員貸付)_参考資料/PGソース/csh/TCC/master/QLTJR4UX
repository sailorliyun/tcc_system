#!/bin/csh
# 　スクリプト名 ： QLTJR3UX
#　 機  能       ： 各データのＬＯＡＤを行う
#　 作成者       ： FIP
#
#  修正番号 修正者   修正日時   修正内容
#  000001   hayashi  2007/07/13 ﾛｰﾀﾞｰ後ｲﾝﾃﾞｯｸｽﾁｪｯｸ追加
#
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start

##################################
#           環境設定             #
##################################
set    SHELL_NAME = $0                             # シェル名
setenv JOB_ID        $SHELL_NAME:t                 # ジョブ名
setenv JOBNET_ID     'QLTK02UX'                    # ジョブネット名
set    LOGFILE       = $USER_LOG_DIR/${JOB_ID}.log # ログファイル名
#set    LOGFILE       = /home/batch/qh/log/new/${JOB_ID}.log # ログファイル名
#000001 hayashi MOD START 20070719
#set    LOGWRITE   = /s040/fip/EXE/QUAJ_MSGLOG.BAT       # ログ出力モジュール
set    LOGWRITE   = $EXE_DIR/QUAJ_MSGLOG.BAT           # ログ出力モジュール
#000001 hayashi MOD END 20070719
setenv USERKEY       'QLTJR4UX'                    # ユーザキー
set    KINOU         = 'Ｂ１帳票ＬＯＡＤ'
##################################
setenv GKKBN          1               #外部パラメータ・概況決算区分(1:通常,2:期末)

set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"     # 開始メッセージの出力

##################################
# STEP-1  Ｂ１帳票データ作成 　　#
##################################
set MODULE = 'qvxb_s000200'
set STEPKINOU = 'Ｂ１帳票データ作成'
$LOGWRITE "$MODULE" I "開始:$STEPKINOU" "$USERKEY"     # 開始メッセージの出力

setenv OUT1FILE $QV_DATA_DIR/qv/data/qvxf_printb1.dat

set PROGRAM = $EXE_DIR/qvxb_s000200.exe
if ( -x $PROGRAM ) then
    setenv NLS_LANG Japanese_Japan.JA16SJIS    # 文字コード体系をSJISにする
    $PROGRAM       >&  $LOGFILE                # プログラム起動
    set rc = $status                           # リターンステータス待避
    setenv NLS_LANG Japanese_Japan.JA16EUC     # 文字コード体系をEUCに戻す
else
    $LOGWRITE  $PROGRAM:t  E  '実行プログラム[$PROGRAM]が存在しない。'
    exit 1                                     # 異常終了
endif

if ( $rc != 0 ) then                           # リターンステータス判定
    set MSG = "失敗:$STEPKINOU RC=[$rc]"
    $LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
    exit  1                                    # 異常終了
endif

$LOGWRITE "$MODULE" I "終了:$STEPKINOU" "$USERKEY"   # 終了メッセージの出力

##################################
# STEP-2 データロード            #
##################################
STEP_2:
set TABLE_NAME    = QVX4_PRINTB1T                   # 対象テーブル名

set MODULE = 'SQL*LOADER'
$LOGWRITE "$MODULE" I "開始:Ｂ１帳票データロード" $USERKEY

#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start
#sqlldr $DB_USERID/$DB_PASSWORD                    \
sqlldr $DB_USERID/$DB_PASSWORD@$ORACLE_SID         \
   control=$LDR_CTRL_DIR/qvxf_printb1.sql         \
   data=$QV_DATA_DIR/qv/data/qvxf_printb1.dat        \
   log=$LDR_LOG_DIR/qvxd_printb1.log              \
   direct=true                                    \
               > $LOGFILE
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi end
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi end

set rc = $status
if ( $rc != 0 ) then
	set MSG = "失敗:Ｂ１帳票データロード RC=[$rc]"
	$LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
	exit  1
endif
# 000001 hayashi add 2007/07/13 start
#----------------------------------------------------------#
#   インデックスエラーのチェック
#----------------------------------------------------------#
rm -f ${LOGFILE}2 ${LOGFILE}3

#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start
#sqlplus -S $DB_USERID/$DB_PASSWORD << EOF > ${LOGFILE}2
sqlplus -S $DB_USERID/$DB_PASSWORD@$ORACLE_SID << EOF > ${LOGFILE}2
set lin 200
select index_name,status from user_indexes
       where table_name = '$TABLE_NAME'  and
             status     = 'UNUSABLE';
quit SQL.SQLCODE
EOF
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi end
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi end

set rc = $status
if ( $rc != 0 ) then
    set MSG = "失敗:データロード(sqlplus) RC=[$rc]"
    $LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
    exit  1
endif
grep "UNUSABLE" `find ${USER_LOG_DIR} -name "${JOB_ID}.log2" -print` > ${LOGFILE}3
if !(-z ${LOGFILE}3 ) then
    set MSG = "失敗:データロード(grep) RC=[$rc]"
    $LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
    exit  1
endif
# 000001 hayashi add 2007/07/13 end
$LOGWRITE "$MODULE" I "終了:Ｂ１帳票データロード" $USERKEY

##########################################
# STEP-99      後始末                    #
##########################################
STEP_99:
set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"     # 終了メッセージの出力

exit 0
