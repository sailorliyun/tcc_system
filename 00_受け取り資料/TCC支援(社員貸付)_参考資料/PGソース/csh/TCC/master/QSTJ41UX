#!/bin/csh
#　スクリプト名  ： QSTJ41UX
#　 機  能       ： 住所テーブル更新（全件置換）
#　 作成者       ： FIP
#
#  修正番号 修正者   修正日時   修正内容
#  000001   hayashi  2007/07/13 ﾛｰﾀﾞｰ後ｲﾝﾃﾞｯｸｽﾁｪｯｸ追加
#  000002   hayashi  2007/08/17 ネット名の修正
#
#　 修正履歴
# 項番   管理番号    修正内容                修正者         修正日
# 000003 0807073     非互換対応              NBC.SHIBUICHI  2009/03/29
# 000003 0808052     非互換対応              NBC.SHIBUICHI  2009/03/29

##################################
#           環境設定             #
##################################
set    SHELL_NAME = $0                                 # シェル名
setenv JOB_ID       $SHELL_NAME:t                      # ジョブ名
#000002 MOD hayashi 2007/08/17 START
#setenv JOBNET_ID   'QSD141UX'                          # ジョブネット名
setenv JOBNET_ID   'QST141UX'                          # ジョブネット名
#000002 MOD hayashi 2007/08/17 END
set    LOGFILE    = $USER_LOG_DIR/${JOB_ID}.log        # ログファイル名
set    LOGWRITE   = $EXE_DIR/QUAJ_MSGLOG.BAT           # ログ出力モジュール
setenv USERKEY      'QSTJ41UX'                         # ユーザキー
set    KINOU      = '住所テーブル更新（全件置換）'
# 000001 hayashi add 2007/07/13 start
set TABLE_NAME = QTA9_JUSHOM            # 対象テーブル名
# 000001 hayashi add 2007/07/13 end

#########################################
# STEP-1                                #
#########################################
set MODULE = 'COBOL'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"     # 開始メッセージの出力

set PROGRAM = $EXE_DIR/qtab_s002800.exe
if ( -x $PROGRAM ) then
    #
    #  ファイル識別子の設定
    setenv INFILE $QJ_DATA_DIR/qt/data/host/merge/qtaf_jyusyoms.hft
    setenv OTFILE $QJ_DATA_DIR/qt/data/svr/dat/qtaf_jyusyoms.dat
    #
    #
    rm $QJ_DATA_DIR/qt/data/svr/dat/qtaf_jyusyoms.dat
    #
    #
    setenv NLS_LANG Japanese_Japan.JA16SJIS      # 文字コード体系をSJISにする
    $PROGRAM                                     # プログラム起動
    set rc = $status                             # リターンステータス待避
    setenv NLS_LANG Japanese_Japan.JA16EUC       # 文字コード体系をEUCに戻す
else
    $LOGWRITE  "$MODULE"  E  "実行プログラム[$PROGRAM]が存在しない。" "$USERKEY"
    exit 1                                       # 異常終了
endif

if ( $rc != 0 ) then                              # リターンステータス判定
    exit  1                                      # 異常終了
endif

$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"     # 終了メッセージの出力


#########################################
# STEP-2                                #
#########################################

set MODULE = 'SQL*LOADER'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"    # 開始メッセージの出力

#外字を含むﾎｽﾄﾃﾞｰﾀを従来型ﾊﾟｽﾛｰﾄﾞでﾛｰﾄﾞ
setenv NLS_LANG Japanese_Japan.JA16SJIS

### SQL*LOADERの実行 ###
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start
#sqlldr $DB_USERID/$DB_PASSWORD                           \
sqlldr $DB_USERID/$DB_PASSWORD@$ORACLE_SID                \
   control=$LDR_CTRL_DIR/qtad_jyusyo.sql               \
   data=$QJ_DATA_DIR/qt/data/svr/dat/qtaf_jyusyoms.dat  \
   log=$LDR_LOG_DIR/qt/qtaf_jyusyoms.log                \
   direct=true                                          \
             > $LOGFILE
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi end
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi end

set rc = $status

#外字を含むﾎｽﾄﾃﾞｰﾀを従来型ﾊﾟｽﾛｰﾄﾞでﾛｰﾄﾞ
setenv NLS_LANG Japanese_Japan.JA16EUC

if ( $rc != 0 ) then
    $LOGWRITE  "$MODULE"  E  "失敗:$KINOU  RC=[$rc]"  "$USERKEY"
    exit  1                                      # 異常終了
endif
# 000001 hayashi add 2007/07/13 start
#----------------------------------------------------------#
#   インデックスエラーのチェック
#----------------------------------------------------------#
rm -f ${LOGFILE}2 ${LOGFILE}3

#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start
#sqlplus -S $DB_USERID/$DB_PASSWORD << EOF > ${LOGFILE}2
sqlplus -S $DB_USERID/$DB_PASSWORD@$ORACLE_SID << EOF > ${LOGFILE}2
set lin 200
select index_name,status from user_indexes
       where table_name = '$TABLE_NAME'  and
             status     = 'UNUSABLE';
quit SQL.SQLCODE
EOF
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi end
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi end

set rc = $status
if ( $rc != 0 ) then
    set MSG = "失敗:データロード(sqlplus) RC=[$rc]"
    $LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
    exit  1
endif
grep "UNUSABLE" `find ${USER_LOG_DIR} -name "${JOB_ID}.log2" -print` > ${LOGFILE}3
if !(-z ${LOGFILE}3 ) then
    set MSG = "失敗:データロード(grep) RC=[$rc]"
    $LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
    exit  1
endif
# 000001 hayashi add 2007/07/13 end
$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"     # 終了メッセージの出力

#########################################
# STEP-3 バックアップ処理               #
#########################################
set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"    # 開始メッセージの出力

cp $QJ_DATA_DIR/qt/data/host/merge/qtaf_jyusyoms.hft  $QJ_DATA_DIR/qt/data/svr/backup/qtaf_jyusyoms.hft.bak

set rc = $status

if ( $rc != 0 ) then
	$LOGWRITE  "$MODULE"  E  "失敗:$KINOU  RC=[$rc]"  "$USERKEY"
	exit  1                                      # 異常終了
endif

cp $QJ_DATA_DIR/qt/data/svr/dat/qtaf_jyusyoms.dat  $QJ_DATA_DIR/qt/data/svr/backup/qtaf_jyusyoms.dat.bak

set rc = $status

if ( $rc != 0 ) then
	$LOGWRITE  "$MODULE"  E  "失敗:$KINOU  RC=[$rc]"  "$USERKEY"
	exit  1                                      # 異常終了
endif

$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"    # 終了メッセージの出力
exit 0                                           # 正常終了

