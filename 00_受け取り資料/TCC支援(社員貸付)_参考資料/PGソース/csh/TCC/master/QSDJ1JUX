#!/bin/csh
#　スクリプト名  ： QSDJ1JUX
#　 機  能       ： 処理コントロールファイル更新（全件置換）
#　 作成者       ： G.O
#
#-------------------------------------------------------------------------
#          修  正  履  歴
# 管理No              修正内容                    担当者      修正日付
# TR0601-005          エラーログ不具合横展開      NBC野村     06.12.15
# 0807073             非互換対応               NBC.SHIBUICHI  2009/03/29
# 0808052             非互換対応               NBC.SHIBUICHI  2009/03/29
#-------------------------------------------------------#

##################################
#           環境設定             #
##################################
set    SHELL_NAME = $0                                 # シェル名
setenv JOB_ID       $SHELL_NAME:t                      # ジョブ名
setenv JOBNET_ID   'QSD021UX'                          # ジョブネット名
set    LOGFILE    = $USER_LOG_DIR/${JOB_ID}.log        # ログファイル名
set    LOGWRITE   = $EXE_DIR/QUAJ_MSGLOG.BAT           # ログ出力モジュール
setenv USERKEY      'QSDJ1JUX'                         # ユーザキー
set    KINOU      = '処理コントロールファイル更新（全件置換）'

#########################################
# STEP-1                                #
#########################################
set MODULE = 'COBOL'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"     # 開始メッセージの出力

set PROGRAM = $EXE_DIR/qtab_s002900.exe
if ( -x $PROGRAM ) then
    #
    #  ファイル識別子の設定
    setenv INFILE $QJ_DATA_DIR/qt/data/host/merge/qtaf_syorictrlt.hft
    setenv OTFILE $QJ_DATA_DIR/qt/data/svr/dat/qtaf_syorictrlt.dat
    #
    #
    rm $QJ_DATA_DIR/qt/data/svr/dat/qtaf_syorictrlt.dat
    #
    #
    setenv NLS_LANG Japanese_Japan.JA16SJIS      # 文字コード体系をSJISにする
    $PROGRAM                                     # プログラム起動
    set rc = $status                             # リターンステータス待避
    setenv NLS_LANG Japanese_Japan.JA16EUC       # 文字コード体系をEUCに戻す
else
    $LOGWRITE  "$MODULE"  E  "実行プログラム[$PROGRAM]が存在しない。" "$USERKEY"
    exit 1                                       # 異常終了
endif

if ( $rc != 0 ) then                              # リターンステータス判定

#TR0601-005 ADD START          06.12.15
   set MSG = "失敗:$KINOU RC=[$rc]"
   $LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
#TR0601-005 ADD END            06.12.15

    exit  1                                      # 異常終了
endif

$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"     # 終了メッセージの出力


#########################################
# STEP-2                                #
#########################################

set MODULE = 'SQL*LOADER'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"    # 開始メッセージの出力

#外字を含むﾎｽﾄﾃﾞｰﾀを従来型ﾊﾟｽﾛｰﾄﾞでﾛｰﾄﾞ
setenv NLS_LANG Japanese_Japan.JA16SJIS

### SQL*LOADERの実行 ###
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start
#sqlldr $DB_USERID/$DB_PASSWORD                           \
sqlldr $DB_USERID/$DB_PASSWORD@$ORACLE_SID                \
   control=$LDR_CTRL_DIR/qtad_syorictrl.sql               \
   data=$QJ_DATA_DIR/qt/data/svr/dat/qtaf_syorictrlt.dat  \
   log=$LDR_LOG_DIR/qt/qtaf_syorictrlt.log                \
             > $LOGFILE
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi end
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi end

set rc = $status

#外字を含むﾎｽﾄﾃﾞｰﾀを従来型ﾊﾟｽﾛｰﾄﾞでﾛｰﾄﾞ
setenv NLS_LANG Japanese_Japan.JA16EUC

if ( $rc != 0 ) then
    $LOGWRITE  "$MODULE"  E  "失敗:$KINOU  RC=[$rc]"  "$USERKEY"
    exit  1                                      # 異常終了
endif

$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"    # 終了メッセージの出力
exit 0                                           # 正常終了

