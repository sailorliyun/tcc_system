#!/bin/csh
#　スクリプト名  ： QHTJ82UX
#　 機  能       ： 期日管理Ｔ更新（作業メモ）
#　 作成者       ： FIP
#　 改造         ： ①2000/06/08 SKIP処理を削除
#
#　 修正履歴
# 項番   管理番号    修正内容                修正者         修正日
# 000001 0807073     非互換対応              NBC.SHIBUICHI  2009/03/29
# 000001 0808052     非互換対応              NBC.SHIBUICHI  2009/03/29

##################################
#           環境設定             #
##################################
set    SHELL_NAME = $0                             # シェル名
setenv JOB_ID       $SHELL_NAME:t                  # ジョブ名
setenv JOBNET_ID   'QHT631UX'                      # ジョブネット名
set    LOGFILE    = $USER_LOG_DIR/${JOB_ID}.log    # ログファイル名
set    LOGWRITE   = $EXE_DIR/QUAJ_MSGLOG.BAT       # ログ出力モジュール
setenv USERKEY      'QHTJ82UX'                     # ユーザキー
set    KINOU      = '期日管理Ｔ更新（作業メモ）'

set    QH_SHIFT_DIR = $QH_DATA_DIR/shift
set    QH_SAVE_DIR  = /d271/UFS/data/qh/shift
set    STEP_F       = $QH_DATA_DIR/${JOB_ID}_STEP_.tmp

set    TABLE_NAME   = QHE1_SAGYOMEMOT                 # 対象テーブル名

set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "開始:$KINOU" "$USERKEY"      # 開始メッセージの出力


#set year  = $1                             # 処理年  入力パラメータ
#set month = $2                             # 処理月  入力パラメータ
set year  = `date +%Y`                     # 処理年 (システム日付より)
set month = `date +%m`                     # 処理月 (システム日付より)

# 処理月Leading Zero Suppress
@ month ++
@ month --

# 前月算出
set zen_year  = $year
set zen_month = $month
if ( $zen_month == 1 ) then
	@ zen_year --
	set zen_month = 12
else
	@ zen_month --
endif

if ( $zen_month < 10 ) then
	set zen_month = 0$zen_month
endif

if ( $month < 10 ) then
	set month = 0$month
endif

#if ( ! -e $STEP_F ) then
#    touch $STEP_F
#endif

##################################
# STEP-1 対象前月検索            #
##################################
set MODULE = 'SQL*PLUS-1'
set STEPKINOU = 'QHE1 対象前月検索'
$LOGWRITE "$MODULE" I "開始:$STEPKINOU" $USERKEY

#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start
#sqlplus -S $DB_USERID/$DB_PASSWORD << EOF > $LOGFILE
sqlplus -S $DB_USERID/$DB_PASSWORD@$ORACLE_SID << EOF > $LOGFILE
set sqlp ""
set sqln off
set pause off
set hea   off
set lin 200
column QHEE_PRMPARTITIONMEI format A50
column QHEE_TABLESPACEMEI format A50
select max( QHEE_BOUNDTI )
       from QHEE_PARTTABLET
       where QHEE_PRMTABLEMEI = '$TABLE_NAME';
quit SQL.SQLCODE
EOF
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi end
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi end

set rc = $status
if ( $rc != 0 ) then
	set MSG = "失敗:$STEPKINOU RC=[$rc]"
	$LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
	exit  1
endif

set val = `grep "^[0-9]" $LOGFILE`
if ( "$val" == "" ) then
	set MSG = "失敗:$STEPKINOU（該当無し） "
	$LOGWRITE  "$MODULE"  E  "$MSG" "$USERKEY"
	exit  1
endif

set ZEN_BOUND = $val[1]

if ( $ZEN_BOUND != $zen_year$zen_month ) then
	set MSG = "失敗:$STEPKINOU 処理年月が間違っています[$ZEN_BOUND]"
	$LOGWRITE  "$MODULE"  E  "$MSG" "$USERKEY"
	exit  1
endif

$LOGWRITE "$MODULE" I "終了:$STEPKINOU" $USERKEY

##########################################
# STEP-2 期日管理テーブル更新            #
##########################################
STEP_KIJITSU_MOD_PROC:

#set cnt = `grep -c "STEP2" $STEP_F`
#if ( $cnt > 0 ) then
#	goto STEP_END_PROC
#endif

set MODULE = 'SQL*PLUS-2'
set STEPKINOU = 'QHE1 期日管理テーブル更新'
$LOGWRITE "$MODULE" I "開始:$STEPKINOU" $USERKEY

#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi start
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi start
#sqlplus -S $DB_USERID/$DB_PASSWORD << EOF > $LOGFILE
sqlplus -S $DB_USERID/$DB_PASSWORD@$ORACLE_SID << EOF > $LOGFILE

update QHEE_PARTTABLET set QHEE_BOUNDTI = '$year$month'
       where QHEE_PRMTABLEMEI = '$TABLE_NAME';
quit SQL.SQLCODE
EOF
#MOD 0807073 クレジット支援システムの再構築 20090329 shibuichi end
#MOD 0808052 ＴＣＣ支援システムの再構築     20090329 shibuichi end

set rc = $status
if ( $rc != 0 ) then
	set MSG = "失敗:$STEPKINOU RC=[$rc]"
	$LOGWRITE  "$MODULE"  E  "$MSG" $USERKEY
	exit  1
endif

#echo "STEP2" >> $STEP_F

$LOGWRITE "$MODULE" I "終了:$STEPKINOU" $USERKEY

##########################################
# STEP-99      後始末                    #
##########################################
STEP_END_PROC:

#/bin/rm $STEP_F

set MODULE = 'SHELL'
$LOGWRITE "$MODULE" I "終了:$KINOU" "$USERKEY"   # 終了メッセージの出力

exit 0
