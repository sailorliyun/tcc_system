      *8...........2.........3.........4.........5........6........7..
000200 IDENTIFICATION                  DIVISION.
       PROGRAM-ID.                     QPAB-S000100.
      ****************************************************************
      *   システムコード       :  Ｑ                                 *
      *   サブシステムコード   :  ＰＡ                               *
      *   モジュール           :  人事マスタデータ抽出処理１         *
      *   作成者               :  ＦＩＰ                             *
      *   作成日               :  H.13.09.25                         *
      *   処理概要                ホストからハルフト転送された       *
      *                           人事マスタデータファイルを         *
      *                           社員番号２をキーにソートし、       *
      *                           ソート後ファイルを作成する。       *
      *                                                              *
      *--------------------------------------------------------------*
      *                  修  正  履  歴                              *
      *   管理NO            修正理由             担当者   修正日付   *
      *                                                              *
      *                                                              *
      ****************************************************************
       ENVIRONMENT                     DIVISION.
       CONFIGURATION                   SECTION.
       SOURCE-COMPUTER.                FACOM.
       OBJECT-COMPUTER.                FACOM.
       SPECIAL-NAMES.
           CONSOLE                     IS  DSP
           ENVIRONMENT-NAME            IS  ENVNM
           ENVIRONMENT-VALUE           IS  ENVVAL.
       INPUT-OUTPUT                    SECTION.
       FILE-CONTROL.
           SELECT  SORT-FILE            ASSIGN  TO    SORTWK.
           SELECT  IN-FILE              ASSIGN  TO    INFILE
                   FILE STATUS          IS STATUS-IN-FILE.
           SELECT  OUT-FILE             ASSIGN  TO    OTFILE
                   FILE STATUS          IS STATUS-OT-FILE.
      /*
       DATA                            DIVISION.
       FILE                            SECTION.
      *********************************
      *    ソート用ワークファイル     *
      *********************************
       SD  SORT-FILE.
       01  SORT-REC.
           03  FILLER                  PIC  X(20).
           03  SORT-KEY                PIC  X(07).
           03  SORT-STR-AREA           PIC  X(93).
      *
      *********************************
      *    入力ハルフトファイル       *
      *********************************
       FD  IN-FILE
           LABEL   RECORD              STANDARD.
       01  IN-REC.
           03  FILLER                  PIC  X(20).
           03  IN-KEY                  PIC  X(07).
           03  IN-STR-AREA             PIC  X(93).
      *
      *********************************
      *    出力ファイル               *
      *********************************
       FD  OUT-FILE
           LABEL   RECORD              STANDARD.
       01  OUT-REC.
           03  STR-AREA1               PIC  X(120).
      *
      /
       WORKING-STORAGE                 SECTION.
      *
      *
      *    ＜  プログラムスイッチ領域  ＞
      *
       01  SW-AREA.
           03  SW-END                  PIC  X(02)  VALUE "  ".
           03  SW-SORT-END             PIC  X(02)  VALUE "  ".
           03  SW-ABEND                PIC  X(02)  VALUE "0".
           03  SW-DATAERR              PIC  X(02)  VALUE "  ".
      *
      *    ＜  ファイルスタータス領域  ＞
      *
       01  STATUS-AREA.
           03  STATUS-ST-FILE          PIC  X(02).
           03  STATUS-OT-FILE          PIC  X(02).
           03  STATUS-IN-FILE          PIC  X(02).
      *
      *    ＜  カウント領域  ＞
      *
       01  CNT-AREA.
           03  CNT-IN-REC              PIC  9(07)  COMP-3  VALUE ZERO.
           03  CNT-OUT-REC             PIC  9(07)  COMP-3  VALUE ZERO.
      *
      *    ＜  メッセージ編集領域  ＞
      *
       01  DSP-MODULEID                PIC  X(12) VALUE "QPAB_S000100".
      *
      *------------------------------------------------*
      *    ＝  人事マスタファイル  OPEN ｴﾗｰ      ＝    *
      *------------------------------------------------*
       01  DSP-ERR1.
           03  FILLER                  PIC  X(30)  VALUE
               "IN  RIYOUDTL     OPEN ERROR : ".
           03  ERR1-STATUS             PIC  X(02).
      *------------------------------------------------*
      *     ＝  人事マスタソートファイル OPEN ｴﾗｰ＝    *
      *------------------------------------------------*
       01  DSP-ERR2.
           03  FILLER                  PIC  X(30)  VALUE
               "OUT RIYOUDTL     OPEN ERROR : ".
           03  ERR2-STATUS             PIC  X(02).
      *------------------------------------------------*
      *    ＝  人事マスタソートファイル  WRITEｴﾗｰ＝    *
      *------------------------------------------------*
       01  DSP-ERR3.
           03  FILLER                  PIC  X(30)  VALUE
               "OUT RIYOUDTL     WRITE ERROR: ".
           03  ERR3-STATUS             PIC  X(02).
      *    ＝  処理開始  ＝
       01  DSP-MSG1.
           03  FILLER                  PIC  X(08)  VALUE
               "START : ".
           03  MSG1-TIME               PIC  X(08).
      *
      *    ＝  処理終了  ＝
       01  DSP-MSG2.
           03  FILLER                  PIC  X(08)  VALUE
               "END   : ".
           03  MSG2-TIME               PIC  X(08).
      *
      *    ＝  処理件数  ＝
       01  DSP-MSG3-IN.
           03  FILLER                  PIC  X(24)  VALUE
               "RIYOUDTL        IN    :".
           03  MSG3-CNT-IN               PIC  ZZ,ZZZ,ZZ9.
      *    ＝  処理件数  ＝
       01  DSP-MSG4-OUT.
           03  FILLER                  PIC  X(24)  VALUE
               "RIYOUDTL        WRITE :".
           03  MSG4-CNT-OUT              PIC  ZZ,ZZZ,ZZ9.
      *
      *
      *    ＝  編集領域  ＝
       01  ED-WORK.
      *          時刻の編集用
           03    WK-TIME               PIC  9(08).
           03    FILLER                REDEFINES   WK-TIME.
             05  WK-TIME-HH            PIC  9(02).
             05  WK-TIME-MM            PIC  9(02).
             05  WK-TIME-SS            PIC  9(02).
           03    WK-ED-TIME.
             05  WK-ED-TIME-HH         PIC  9(02).
             05  FILLER                PIC  X(01)  VALUE ":".
             05  WK-ED-TIME-MM         PIC  9(02).
             05  FILLER                PIC  X(01)  VALUE ":".
             05  WK-ED-TIME-SS         PIC  9(02).
      *      日付の編集用            *
           03    WK-DATE             PIC  9(08).
           03    FILLER              REDEFINES   WK-DATE.
             05  WK-DATE-YYYY        PIC  9(04).
             05  WK-DATE-MM          PIC  9(02).
             05  WK-DATE-DD          PIC  9(02).
           03    WK-ED-DATE.
             05  WK-ED-DATE-YYYY     PIC  9(04).
             05  FILLER              PIC  X(01)  VALUE "/".
             05  WK-ED-DATE-MM       PIC  9(02).
             05  FILLER              PIC  X(01)  VALUE "/".
             05  WK-ED-DATE-DD       PIC  9(02).
      *
      *
       01  WK-SQLERRML               PIC S9(04)
                                     SIGN LEADING SEPARATE.
       01  WK-SQLERRML-X             PIC  X(05).
      *
       01  WK-AREA.
           03    WK-IDX              PIC  9(07)  COMP-3  VALUE ZERO.
           03    WK-IDX-M            PIC  9(07)  COMP-3  VALUE ZERO.
           03    WK-SYSTEM-C         PIC  X(12)  VALUE "SYSTEM".
           03    WK-SYSTEM           REDEFINES WK-SYSTEM-C.
             05  WK-SYSTEM-N         PIC  N(06).
           03    WK-KIJITU.
             05  WK-KIJITU-YM        PIC  9(06).
      *
      ****************************************************************
      * ORACLE HOST変数
      ****************************************************************
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.
       01  HOST-WORK.
           03  SQLSTATE                 PIC  X(005).
      *
       01  HOST-CONNECT.
           03  ENV-ORAUID               PIC  X(016).
           03  ENV-ORAPWD               PIC  X(016).
           03  ENV-ORASID               PIC  X(016).
           03  ENV-JOBNETMEI            PIC  X(008).
           03  ENV-JOBMEI               PIC  X(008).
           03  ENV-USERKEY              PIC  X(020).
      *
       01  HOST-TOROKU-DATE-TIME.
           03  HOST-TOROKU-DATE         PIC  X(08).
           03  HOST-TOROKU-TIME         PIC  X(06).
      *
       01  HOST-QSAP-DISPUPONCONS.
           03  QSAP-MODULE-ID           PIC  X(020).
           03  QSAP-MSG-KBN             PIC  X(001).
           03  QSAP-MSG                 PIC  X(1024).
      *
       01  HWK-KIJITSUYM-M              PIC  X(006).
       01  HWK-KIJITSUYM-R              PIC  X(006).
       01  HWK-TABLEMEI                 PIC  X(020).
           EXEC SQL END DECLARE SECTION END-EXEC.
      ****************************************************************
      * ORACLE SQL COMMUNICATIONS AREA
      ****************************************************************
           EXEC SQL INCLUDE SQLCA.COB END-EXEC.
      *
      *********************************************
      *    ＬＩＮＫ−ＣＯＮＮＥＣＴ−ＡＲＥＡ     *
      *********************************************
       01  LINK-CONNECT-AREA.
           03  LINK-SQLCODE            PIC S9(09) COMPUTATIONAL.
           03  LINK-USERID             PIC  X(10).
           03  LINK-USERID-LEN         PIC  9(02).
           03  LINK-PASSWD             PIC  X(10).
           03  LINK-PASSWD-LEN         PIC  9(02).
           03  LINK-ORACLE             PIC  X(10).
           03  LINK-ORACLE-LEN         PIC  9(02).
      *
      **********************************************
      *    コンソールログ出力サブ・リンクエリア    *
      **********************************************
       01  LINK-CONS-AREA.
           03  LINK-CONS-MODULEID      PIC  X(40).
           03  LINK-CONS-MSGKBN        PIC  X(01).
           03  LINK-CONS-MSG           PIC  X(1024).
           03  LINK-CONS-STATUS        PIC  9(02).
      /
       PROCEDURE                     DIVISION.
      ****************************************************************
      *    (0.0)     プログラム  コントロール                        *
      ****************************************************************
       PROC-SECT                     SECTION.
       PROC-START.
      *
      *              初期処理  [1.0]
      *
           PERFORM   INIT-PROC.
      *
      *              主処理    [2.0]
      *
           PERFORM   MAIN-PROC.
      *
      *              終了処理  [3.0]
      *
           PERFORM   FINAL-PROC.
      *
       PROC-EXIT.
           EXIT       PROGRAM.
      /***************************************************************
      *    (1.0)     初期処理                                        *
      ****************************************************************
       INIT-PROC                     SECTION.
           EXEC   SQL
               WHENEVER    SQLWARNING  DO  PERFORM  ORA-ERR-PROC
           END-EXEC.
      *
           EXEC   SQL
               WHENEVER    SQLERROR    DO  PERFORM  ORA-ERR-PROC
           END-EXEC.
      *
           EXEC   SQL
               WHENEVER NOT FOUND CONTINUE
           END-EXEC.
      *
           MOVE    DSP-MODULEID         TO    LINK-CONS-MODULEID.
      *
      *-----<< ORACLE CONNECT >>-----*
           CALL    "QTAB-CONNECT"      USING  LINK-CONNECT-AREA.
      *
           IF      LINK-SQLCODE        NOT =   ZERO
      *-----<< ORACLE CONNECT ERR   END MESSAGE >>-----*
               PERFORM TIME-EDIT-PROC
               MOVE    WK-ED-TIME       TO    MSG2-TIME
               MOVE    "I"              TO    LINK-CONS-MSGKBN
               MOVE    DSP-MSG2         TO    LINK-CONS-MSG
      *
               PERFORM CONS-PROC
               MOVE    099              TO    PROGRAM-STATUS
               EXIT    PROGRAM
           END-IF.
      *
      *-----<< START MESSAGE >>-----*
      *
           PERFORM TIME-EDIT-PROC.
           MOVE    WK-ED-TIME           TO    MSG1-TIME.
           MOVE    "I"                  TO    LINK-CONS-MSGKBN.
           MOVE    DSP-MSG1             TO    LINK-CONS-MSG.
           PERFORM CONS-PROC.
      *
      **-----<< TOUROKU DATE TIME >>-----*
           EXEC SQL
               SELECT
                   TO_CHAR(SYSDATE,'YYYYMMDD'),
                   TO_CHAR(SYSDATE,'HH24MISS')
               INTO
                   :HOST-TOROKU-DATE,
                   :HOST-TOROKU-TIME
               FROM DUAL
           END-EXEC.
      *
       INIT-PROC-EXIT.
           EXIT.
      /***************************************************************
      *    (2.0)     主処理                                          *
      ****************************************************************
       MAIN-PROC                     SECTION.
      *
           SORT  SORT-FILE  ON
               ASCENDING  SORT-KEY
           USING   IN-FILE
           GIVING  OUT-FILE.
      *
       MAIN-PROC-EXIT.
           EXIT.
      /***************************************************************
      *    (3.0)     終了処理                                        *
      ****************************************************************
       FINAL-PROC                    SECTION.
      *    処理終了メッセージ出力
           PERFORM TIME-EDIT-PROC.
           MOVE    WK-ED-TIME            TO    MSG2-TIME.
           MOVE    DSP-MSG2              TO    LINK-CONS-MSG.
      *    コンソールログ出力処理
           PERFORM CONS-PROC.
      *
      *---------------------------------------------------------------*
           IF  ( SW-ABEND   =  "0" ) 
                   MOVE    000           TO      PROGRAM-STATUS
           ELSE
               IF  SW-DATAERR            =      "1"
                   MOVE    012           TO      PROGRAM-STATUS
               ELSE
                   MOVE    050           TO      PROGRAM-STATUS
               END-IF
           END-IF.
      *
       FINAL-PROC-EXIT.
           EXIT.
      /***************************************************************
      *    (C.0)   コンソールログ出力処理
      ****************************************************************
       CONS-PROC                       SECTION.
      *    ＜＜コンソールログ出力サブ＞＞
           CALL    "QTAB-CONSLOG"      USING   LINK-CONS-AREA.
           IF      LINK-CONS-STATUS    =       ZERO
      *
      *            ＜＜データベーストランザクションコミット＞＞
                   EXEC SQL COMMIT WORK END-EXEC
                   MOVE    000         TO      PROGRAM-STATUS
           ELSE
                   MOVE    099         TO      PROGRAM-STATUS
                   EXIT    PROGRAM
           END-IF.
      *
       CONS-PROC-EXIT.
           EXIT.
      /***************************************************************
      *    (C.1)   時間編集
      ****************************************************************
       TIME-EDIT-PROC                   SECTION.
      *
           ACCEPT  WK-TIME             FROM    TIME.
           MOVE    WK-TIME-HH          TO      WK-ED-TIME-HH.
           MOVE    WK-TIME-MM          TO      WK-ED-TIME-MM.
           MOVE    WK-TIME-SS          TO      WK-ED-TIME-SS.
      *
       TIME-EDIT-PROC-EXIT.
           EXIT.
      /***************************************************************
      *    (C.2)   ORACLE ERR
      ****************************************************************
       ORA-ERR-PROC                  SECTION.
           MOVE    "E"                 TO      LINK-CONS-MSGKBN.
           MOVE    SQLERRML            TO      WK-SQLERRML.
           MOVE    WK-SQLERRML         TO      WK-SQLERRML-X.
           STRING  WK-SQLERRML-X " " SQLERRMC
                   DELIMITED  BY  "*"  INTO    LINK-CONS-MSG.
      *    コンソールログ出力処理
           PERFORM CONS-PROC.
      *
           MOVE    099                 TO      PROGRAM-STATUS.
           EXIT    PROGRAM.
       ORA-ERR-PROC-EXIT.
           EXIT.
      /***************************************************************
      *    (C.3)     ＳＱＬエラー                                    *
      ****************************************************************
       ERR-QHAE-PROC                 SECTION.
      *
           MOVE    "1"                 TO      SW-ABEND SW-END.
      *
           MOVE    "E"                 TO      LINK-CONS-MSGKBN.
           MOVE    SQLERRML            TO      WK-SQLERRML.
           MOVE    WK-SQLERRML         TO      WK-SQLERRML-X.
           STRING  "QHAE_PARTTABLE " WK-SQLERRML-X " " SQLERRMC
                   DELIMITED  BY  "*"  INTO    LINK-CONS-MSG.
      *    コンソールログ出力処理
           PERFORM CONS-PROC.
      *
           MOVE    099                 TO      PROGRAM-STATUS.
           EXIT    PROGRAM.
      *
       ERR-QHAE-PROC-EXIT.
           EXIT.
