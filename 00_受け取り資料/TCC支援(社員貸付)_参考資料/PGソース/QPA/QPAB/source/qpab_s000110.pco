      *8...........2.........3.........4.........5........6........7..
000200 IDENTIFICATION                  DIVISION.
       PROGRAM-ID.                     QPAB-S000110.
      ****************************************************************
      *   システムコード       :  Ｑ                                 *
      *   サブシステムコード   :  ＰＡ                               *
      *   モジュール           :  社員マスタテーブルデータ抽出       *
      *                                                              *
      *   作成者               :  ＦＩＰ                             *
      *   作成日               :  H.13.10.19                         *
      *   処理概要             :  社員マスタから社員番号１が         *
      *                           スペースのデータを抽出する。       *
      *                                                              *
      *                                                              *
      *--------------------------------------------------------------*
      *                  修  正  履  歴                              *
      *   管理NO            修正理由             担当者   修正日付   *
      *                                                              *
      *                                                              *
      ****************************************************************
       ENVIRONMENT                     DIVISION.
       CONFIGURATION                   SECTION.
       SOURCE-COMPUTER.                FACOM.
       OBJECT-COMPUTER.                FACOM.
       SPECIAL-NAMES.
           CONSOLE                     IS  DSP
           ENVIRONMENT-NAME            IS  ENVNM
           ENVIRONMENT-VALUE           IS  ENVVAL.
       INPUT-OUTPUT                    SECTION.
       FILE-CONTROL.
      *##########################################################
      *    ＜  ファイル定義  ＞
      *##########################################################
           SELECT  OT-FILE             ASSIGN  TO    OTFILE
                   FILE STATUS         IS STATUS-OT-FILE.
      /*
       DATA                            DIVISION.
       FILE                            SECTION.
      *##########################################################
      *    ＜  出力ファイル項目定義  ＞
      *##########################################################
       FD  OT-FILE
           LABEL   RECORD              STANDARD.
       01  OT-REC.
           03  OT-STR.
             05  OT-PRMSYAINCD         PIC  X(01).
             05  OT-PRMSYAINCD2        PIC  X(07).
             05  OT-SIMEIKANA          PIC  X(15).
             05  OT-SIMEIKANJI         PIC  X(20).
             05  OT-MISECD             PIC  X(03).
             05  OT-TOZAIKBN           PIC  X(01).
             05  OT-BUMONCD            PIC  X(06).
             05  OT-SIKAKUCD           PIC  X(02).
             05  OT-TOKYUCD            PIC  X(02).
             05  OT-BORNYMD            PIC  X(08).
             05  OT-TAISYOKUYMD        PIC  X(08).
             05  OT-KYUSYOSTYMD        PIC  X(08).
             05  OT-KYUSYOEDYMD        PIC  X(08).
      *  固定項目（制御コード用）
           03  OT-CR                   PIC  X(01).
      *
      *
      /
      ****************************************************************
      *    編集項目定義                                              #
      ****************************************************************
       WORKING-STORAGE                 SECTION.
      *
      *##########################################################
      *    ＜  編集領域  ＞
      *##########################################################
      *
      *
      *
      *##########################################################
      *    ＜  プログラムスイッチ領域  ＞
      *##########################################################
      *
       01  SW-AREA.
           03  SW-ABEND                PIC  X(01)  VALUE "0".
      *
      *##########################################################
      *    ＜  ファイルスタータス領域  ＞
      *##########################################################
      *
       01  STATUS-AREA.
           03  STATUS-OT-FILE          PIC  X(02).
      *
      *##########################################################
      *    ＜  カウント領域  ＞
      *##########################################################
      *
       01  CNT-AREA.
      *    総データ出力件数
           03  CNT-OTREC            PIC  9(07)  COMP-3  VALUE ZERO.
      *
      *##########################################################
      *    ＜  メッセージ編集領域  ＞
      *##########################################################
      *
       01  DSP-MODULEID             PIC  X(12)  VALUE "QPAB_S000110".
      *
      *##########################################################
      *    ＝  処理開始  ＝
      *##########################################################
      *
       01  DSP-MSG1.
           03  FILLER                  PIC  X(08)  VALUE
               "START : ".
           03  MSG1-TIME               PIC  X(08).
      *
      *##########################################################
      *    ＝  処理終了  ＝
      *##########################################################
      *
       01  DSP-MSG2.
           03  FILLER                  PIC  X(08)  VALUE
               "END   : ".
           03  MSG2-TIME               PIC  X(08).
      *
      *##########################################################
      *    ＝  処理件数  ＝
      *##########################################################
      *
       01  DSP-MSG3.
           03  FILLER                  PIC  X(08)  VALUE
               "OUT   : ".
           03  MSG3-CNT1               PIC  ZZ,ZZZ,ZZ9.
      *
      *##########################################################
      *    ＝  ファイルへの書込みエラー  ＝
      *##########################################################
      *
       01  DSP-ERR1.
           03  FILLER                  PIC  X(19)  VALUE
               "FILE WRITE ERROR : ".
           03  ERR1-STATUS             PIC  X(02).
      *
      *##########################################################
      *    ＝  編集領域  ＝
      *##########################################################
      *
       01  ED-WORK.
      *          時刻の編集用
           03    WK-TIME               PIC  9(08).
           03    FILLER                REDEFINES   WK-TIME.
             05  WK-TIME-HH            PIC  9(02).
             05  WK-TIME-MM            PIC  9(02).
             05  WK-TIME-SS            PIC  9(02).
           03    WK-ED-TIME.
             05  WK-ED-TIME-HH         PIC  9(02).
             05  FILLER                PIC  X(01)  VALUE ":".
             05  WK-ED-TIME-MM         PIC  9(02).
             05  FILLER                PIC  X(01)  VALUE ":".
             05  WK-ED-TIME-SS         PIC  9(02).
      *
      ****************************************************************
      * ORACLE HOST変数
      ****************************************************************
      *
       01  WK-SQLERRML                 PIC S9(04)
                                       SIGN LEADING SEPARATE.
       01  WK-SQLERRML-X               PIC  X(05).
      *
      *    埋込みＳＱＬ宣言開始（Ｂ領域：１２桁目以降）
      *
           EXEC SQL INCLUDE SQLCA END-EXEC.
           EXEC SQL INCLUDE ORACA END-EXEC.
           EXEC ORACLE OPTION (ORACA=YES) END-EXEC.
      *
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.
      *
      *##########################################################
      *#  社員マスタテーブルデータ項目エリア
      *##########################################################
      *
       01  ORA-REC.
           03  ORA-PRMSYAINCD           PIC  X(01).
           03  ORA-PRMSYAINCD2          PIC  X(07).
           03  ORA-SIMEIKANA            PIC  X(15).
           03  ORA-SIMEIKANJI           PIC  X(20).
           03  ORA-MISECD               PIC  X(03).
           03  ORA-TOZAIKBN             PIC  X(01).
           03  ORA-BUMONCD              PIC  X(06).
           03  ORA-SIKAKUCD             PIC  X(02).
           03  ORA-TOKYUCD              PIC  X(02).
           03  ORA-BORNYMD              PIC  X(08).
           03  ORA-TAISYOKUYMD          PIC  X(08).
           03  ORA-KYUSYOSTYMD          PIC  X(08).
           03  ORA-KYUSYOEDYMD          PIC  X(08).
      *
      *
      *
      ****************************************************************
      *    埋込みＳＱＬ宣言終了
      ****************************************************************
           EXEC SQL  END  DECLARE SECTION END-EXEC.
      *
      *
      *
      *
      *##########################################################
      *    ＜  その他のワーク領域  ＞
      *##########################################################
      *
       01  LINK-CONNECT-AREA.
           03  LINK-SQLCODE            PIC S9(09)  COMPUTATIONAL.
           03  LINK-USERID             PIC  X(10).
           03  LINK-USERID-LEN         PIC  9(02).
           03  LINK-PASSWD             PIC  X(10).
           03  LINK-PASSWD-LEN         PIC  9(02).
           03  LINK-ORACLE             PIC  X(10).
           03  LINK-ORACLE-LEN         PIC  9(02).
      *
      *
      *##########################################################
      *    コンソールログ出力サブ・リンクエリア
      *##########################################################
      *
       01  LINK-CONS-AREA.
           03  LINK-CONS-MODULEID      PIC  X(40).
           03  LINK-CONS-MSGKBN        PIC  X(01).
           03  LINK-CONS-MSG           PIC  X(1024).
           03  LINK-CONS-STATUS        PIC  9(02).
      *
      /
       PROCEDURE                       DIVISION.
      ****************************************************************
      *    (0.0)   プログラム  コントロール                          *
      ****************************************************************
       PROC-SECT                       SECTION.
       PROC-START.
      *
      *＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊
      *＊　初期処理  [1.0] 　　　　　　　　　　　　　　　　　　　　＊
      *＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊
      *
           PERFORM INIT-PROC.
      *
      *＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊
      *＊　主処理    [2.0] 　　　　　　　　　　　　　　　　　　　　＊
      *＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊
      *
           PERFORM MAIN-PROC.
      *
      *＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊
      *＊　終了処理  [3.0] 　　　　　　　　　　　　　　　　　　　　＊
      *＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊
      *
           PERFORM FINAL-PROC.
      *
       PROC-EXIT.
           EXIT    PROGRAM.
      /***************************************************************
      *    (1.0)   初期処理                                          *
      ****************************************************************
       INIT-PROC                       SECTION.
      *##########################################################
      *#　初期化が必要なワーク領域の初期化を行う　　　　　　　　#
      *##########################################################
      *
           MOVE    DSP-MODULEID        TO      LINK-CONS-MODULEID.
      *
      *##########################################################
      *    オラクルコネクト処理
      *##########################################################
      *
           CALL    "QTAB-CONNECT"      USING   LINK-CONNECT-AREA.
           IF      LINK-SQLCODE        NOT =   ZERO
      *            処理開始メッセージ出力
                   ACCEPT  WK-TIME     FROM    TIME
                   MOVE    WK-TIME-HH  TO      WK-ED-TIME-HH
                   MOVE    WK-TIME-MM  TO      WK-ED-TIME-MM
                   MOVE    WK-TIME-SS  TO      WK-ED-TIME-SS
                   MOVE    WK-ED-TIME  TO      MSG1-TIME
                   MOVE    "I"         TO      LINK-CONS-MSGKBN
                   MOVE    DSP-MSG1    TO      LINK-CONS-MSG
      *            コンソールログ出力処理
                   PERFORM CONS-PROC
      *            処理終了メッセージ出力
                   ACCEPT  WK-TIME     FROM    TIME
                   MOVE    WK-TIME-HH  TO      WK-ED-TIME-HH
                   MOVE    WK-TIME-MM  TO      WK-ED-TIME-MM
                   MOVE    WK-TIME-SS  TO      WK-ED-TIME-SS
                   MOVE    WK-ED-TIME  TO      MSG2-TIME
                   MOVE    "I"         TO      LINK-CONS-MSGKBN
                   MOVE    DSP-MSG2    TO      LINK-CONS-MSG
      *            コンソールログ出力処理
                   PERFORM CONS-PROC
                   MOVE    099         TO      PROGRAM-STATUS
                   EXIT    PROGRAM
           END-IF.
      *
      *##########################################################
      *    処理開始メッセージ出力
      *##########################################################
      *
           ACCEPT  WK-TIME             FROM    TIME.
           MOVE    WK-TIME-HH          TO      WK-ED-TIME-HH.
           MOVE    WK-TIME-MM          TO      WK-ED-TIME-MM.
           MOVE    WK-TIME-SS          TO      WK-ED-TIME-SS.
           MOVE    WK-ED-TIME          TO      MSG1-TIME.
           MOVE    "I"                 TO      LINK-CONS-MSGKBN.
           MOVE    DSP-MSG1            TO      LINK-CONS-MSG.
      *    コンソールログ出力処理
           PERFORM CONS-PROC.
      *
      *
      *##########################################################
      *#    出力ファイルオープン                                #
      *##########################################################
      *
           OPEN   OUTPUT                OT-FILE.
           IF  ( STATUS-OT-FILE NOT  =    ZERO )
               MOVE    "01"             TO    DSP-ERR1(14:2)
               MOVE    STATUS-OT-FILE   TO    ERR1-STATUS
               MOVE    099              TO    PROGRAM-STATUS
               EXIT    PROGRAM
           END-IF.
      *
      *
      *##########################################################
      *　　初期値設定
      *##########################################################
      *
      *
      *
       INIT-PROC-EXIT.
           EXIT.
      *
      *
      *
      *
      /***************************************************************
      *    (2.0)   主処理                                            *
      ****************************************************************
       MAIN-PROC                       SECTION.
      *
      *##########################################################
      *    オラクル使用時の定義                                 *
      *##########################################################
      *    ＳＱＬエラー
           EXEC SQL WHENEVER SQLERROR  GO TO :ERR-QSA7 END-EXEC.
      *
      *    該当データ無し
           EXEC SQL WHENEVER NOT FOUND GO TO :INV-QSA7 END-EXEC.
      *
      *##########################################################
      *#　カーソル宣言を行う　　　　　　　　　　　　　　　　　　#
      *##########################################################
      *
      *    カーソル宣言（控除テーブル）
           EXEC SQL DECLARE CUR_QPA CURSOR FOR
              SELECT
                   QPAC_PRMSYAINCD1
                  ,QPAC_PRMSYAINCD2
                  ,NVL(QPAC_SIMEIKANA, ' ')
                  ,NVL(QPAC_SIMEIKANJI, ' ')
                  ,NVL(QPAC_MISECD, ' ')
                  ,NVL(QPAC_TOZAIKBN, ' ')
                  ,NVL(QPAC_BUMONCD, ' ')
                  ,NVL(QPAC_SIKAKUCD, ' ')
                  ,NVL(QPAC_TOKYUCD, ' ')
                  ,NVL(TO_CHAR(QPAC_BORNYMD,'YYYYMMDD'), ' ')
                  ,NVL(TO_CHAR(QPAC_TAISYOKUYMD,'YYYYMMDD'), ' ')
                  ,NVL(TO_CHAR(QPAC_KYUSYOSTYMD,'YYYYMMDD'), ' ')
                  ,NVL(TO_CHAR(QPAC_KYUSYOEDYMD,'YYYYMMDD'), ' ')
              FROM
                   QPAC_SYAINM
              WHERE
                   QPAC_PRMSYAINCD1 = ' '
           END-EXEC.
      *
      *
      *##########################################################
      *    カーソルＯＰＥＮ
      *##########################################################
      *
           EXEC SQL OPEN CUR_QPA END-EXEC.
      *
      *##########################################################
      *    データ抽出（テーブルを読み終えるまで繰り返し処理する）
      *##########################################################
      *
           PERFORM NO LIMIT
                   MOVE  SPACE    TO  ORA-REC
                   EXEC SQL
                       FETCH CUR_QPA
                       INTO
                          ORA-PRMSYAINCD 
                         ,ORA-PRMSYAINCD2
                         ,ORA-SIMEIKANA
                         ,ORA-SIMEIKANJI 
                         ,ORA-MISECD     
                         ,ORA-TOZAIKBN   
                         ,ORA-BUMONCD    
                         ,ORA-SIKAKUCD   
                         ,ORA-TOKYUCD    
                         ,ORA-BORNYMD    
                         ,ORA-TAISYOKUYMD
                         ,ORA-KYUSYOSTYMD
                         ,ORA-KYUSYOEDYMD
      *
                   END-EXEC
      *
      *##########################################################
      *            抽出データ出力処理
      *##########################################################
      *
                   PERFORM DATA-WRITE
                   IF      SW-ABEND   NOT =   "0"
                           GO  TO              INV-QSA7
                   END-IF
           END-PERFORM.
      *
      *##########################################################
      *    カーソルＣＬＯＳＥ
      *##########################################################
      *
           EXEC SQL CLOSE CUR_QPA END-EXEC.
      *
      *##########################################################
      *    正常時はこのセクションを終了する
      *##########################################################
      *
           GO  TO                      MAIN-PROC-EXIT.
      *
      *##########################################################
      *    テーブル読み込み異常時の処理
      *##########################################################
      *
       ERR-QSA7.
      *
           MOVE    "1"                 TO      SW-ABEND.
      *
           MOVE    "E"                 TO      LINK-CONS-MSGKBN.
           MOVE    SQLERRML            TO      WK-SQLERRML.
           MOVE    WK-SQLERRML         TO      WK-SQLERRML-X.
           STRING  "QPA0_ZANDAKAT " WK-SQLERRML-X " " SQLERRMC
                   DELIMITED  BY  "*"  INTO    LINK-CONS-MSG.
      *    コンソールログ出力処理
           PERFORM CONS-PROC.
      *
            GO  TO                      MAIN-PROC-EXIT.
      *
      *##########################################################
      *    正常時ではこのセクションを終了する
      *    （カーソルもクローズする）
      *##########################################################
      *
       INV-QSA7.
      *
           EXEC SQL CLOSE CUR_QPA END-EXEC
           GO  TO                      MAIN-PROC-EXIT.
      *
       MAIN-PROC-EXIT.
           EXIT.
      *
      *
      *
      *
      /***************************************************************
      *    (2.2)   抽出データ出力                                    *
      ****************************************************************
       DATA-WRITE                      SECTION.
      *
      *##########################################################
      *    抽出データを出力ファイル領域へ
      *##########################################################
      *
           MOVE  ORA-REC               TO  OT-STR.
      *
      *
      *##########################################################
      *    制御コード設定
      *##########################################################
           MOVE    X"0A"               TO  OT-CR.
      *
      *##########################################################
      *    抽出データを出力ファイルに書込
      *##########################################################
      *
           WRITE  OT-REC.
      *
      *
      *##########################################################
      *    書込みエラー判定
      *##########################################################
      *
           IF     STATUS-OT-FILE      NOT =   ZERO
                   MOVE    STATUS-OT-FILE      TO      ERR1-STATUS
                   MOVE    "S"         TO      LINK-CONS-MSGKBN
                   MOVE    DSP-ERR1    TO      LINK-CONS-MSG
      * コンソールログ出力処理
                   PERFORM CONS-PROC
                   MOVE    "1"         TO      SW-ABEND
                   GO TO   DATA-WRITE-EXIT
           END-IF.
      *
      *##########################################################
      *    出力件数のカウント
      *##########################################################
           ADD     1                   TO      CNT-OTREC.
      *
       DATA-WRITE-EXIT.
           EXIT.
      *
      *
      *
      *
      /***************************************************************
      *    (3.0)   終了処理                                          *
      ****************************************************************
       FINAL-PROC                      SECTION.
      *
      *##########################################################
      *    ファイルＣＬＯＳＥ
      *##########################################################
      *
           CLOSE   OT-FILE.
      *
      *##########################################################
      *    処理件数メッセージ出力
      *##########################################################
      *
           MOVE    CNT-OTREC           TO      MSG3-CNT1.
           MOVE    "I"                 TO      LINK-CONS-MSGKBN.
           MOVE    DSP-MSG3            TO      LINK-CONS-MSG.
      *    コンソールログ出力処理
           PERFORM CONS-PROC.
      *
      *##########################################################
      *    処理終了メッセージ出力
      *##########################################################
      *
           ACCEPT  WK-TIME             FROM    TIME.
           MOVE    WK-TIME-HH          TO      WK-ED-TIME-HH.
           MOVE    WK-TIME-MM          TO      WK-ED-TIME-MM.
           MOVE    WK-TIME-SS          TO      WK-ED-TIME-SS.
           MOVE    WK-ED-TIME          TO      MSG2-TIME.
           MOVE    "I"                 TO      LINK-CONS-MSGKBN.
           MOVE    DSP-MSG2            TO      LINK-CONS-MSG.
      *    コンソールログ出力処理
           PERFORM CONS-PROC.
      *
      *##########################################################
      *    処理終了時のプログラムステータスの設定
      *##########################################################
      *
           IF      SW-ABEND            =       "0"
                   MOVE    000         TO      PROGRAM-STATUS
           ELSE
                   MOVE    050         TO      PROGRAM-STATUS
           END-IF.
      *
       FINAL-PROC-EXIT.
           EXIT.
      /***************************************************************
      *    (C.0)   コンソールログ出力処理
      ****************************************************************
       CONS-PROC                       SECTION.
      *
      *##########################################################
      *    コンソールログ出力サブ
      *##########################################################
      *
           CALL    "QTAB-CONSLOG"      USING   LINK-CONS-AREA.
           IF      LINK-CONS-STATUS    =       ZERO
      *
      *            データベーストランザクションコミット
      *             EXEC SQL COMMIT WORK END-EXEC
                   MOVE    000         TO      PROGRAM-STATUS
           ELSE
                   MOVE    099         TO      PROGRAM-STATUS
                   EXIT    PROGRAM
           END-IF.
      *
       CONS-PROC-EXIT.
           EXIT.
